import org.springframework.boot.gradle.tasks.run.BootRun

buildscript {
    dependencies {
        classpath (group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1')
    }
}

plugins {
    id("org.springframework.boot")
    id("io.spring.dependency-management")

    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.spring" version "1.6.21"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.6.21"

    id 'com.bmuschko.docker-spring-boot-application' version '6.7.0'
    id "io.swagger.core.v3.swagger-gradle-plugin" version "2.2.2"
}

configurations {
    provided
    devClasspath
    prodClasspath
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

dependencies {
    implementation project(":core")
    implementation project(":default-security")

    implementation("org.springframework.boot:spring-boot-starter-jersey")
    implementation("org.springframework.boot:spring-boot-starter-security")

    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation ('com.auth0:java-jwt:4.0.0')

    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.jetbrains.kotlin:kotlin-stdlib")

    implementation("org.glassfish.jersey.media:jersey-media-multipart:2.35")
    implementation('io.swagger.core.v3:swagger-annotations:2.2.2')

    devClasspath("com.h2database:h2")
    prodClasspath group: 'mysql', name: 'mysql-connector-java', version: '8.0.18'

    /* Testing */
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
    }
    testImplementation("org.springframework.security:spring-security-test")
    testImplementation("com.h2database:h2")
}

java.sourceCompatibility = JavaVersion.VERSION_11

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "11"
    }
}

test {
    useJUnitPlatform {
        excludeTags 'db'
    }
}

resolve {
    // careful: this is coupled with 'describe' endpoint
    outputFileName = 'openapi-center-backend'

    outputFormat = 'yaml'
    prettyPrint = true
    classpath = sourceSets.main.runtimeClasspath
    resourcePackages = ['io.rss.apicenter.server.presentation.resource']
    outputDir = file(project.buildDir.toPath().resolve('resources/main'))

    // unfortunately not working with yaml
    openApiFile = file(project.projectDir.toPath().resolve('src/main/resources/swagger/openapi-config.json'))
}
bootJar.dependsOn('resolve')

docker {

    springBootApplication {
        baseImage = 'amazoncorretto:11-alpine3.15'
        ports = [8080]
        images = ['oac-server:latest']
        jvmArgs = []
    }
}

// ------------------------------------ Env settings ----------------------------------

def h2DB = [
        DB_CLASSNAME: 'org.h2.Driver',
        SPRING_DATASOURCE_URL: 'jdbc:h2:mem:openapi-center',
        HIBERNATE_DIALECT: 'org.hibernate.dialect.H2Dialect',
        SPRING_DATASOURCE_USERNAME: 'sa',
        SPRING_DATASOURCE_PASSWORD: "''"
];

def mySqlDB = [
        DB_CLASSNAME: 'com.mysql.jdbc.Driver',
        HIBERNATE_DIALECT: 'org.hibernate.dialect.MySQL5InnoDBDialect',
        SPRING_DATASOURCE_URL: 'jdbc:mysql://localhost:3306/oac',
        SPRING_DATASOURCE_USERNAME: 'root',
        SPRING_DATASOURCE_PASSWORD: 'super-secret'
];

// ------------------------------------ Env-dependent tasks ----------------------------------

task copyTestResource(type: Copy) {
    from "${project.projectDir}/src/test/resources"
    into "${project.buildDir}/resources/main"

    doFirst {
        print "Copying files for testing..."
    }
}

// With MySQL
task buildProduction(group: 'docker') {

    doFirst {
        sourceSets {
            main { runtimeClasspath += configurations.prodClasspath }
        }

        dockerSyncBuildContext {
            into ('libs') {
                from configurations.prodClasspath
            }
        }

        dockerCreateDockerfile {
            environmentVariable 'DB_CLASSNAME', 'com.mysql.jdbc.Driver'
            environmentVariable 'HIBERNATE_DIALECT', 'org.hibernate.dialect.MySQL5InnoDBDialect'

            label(['maintainer': 'Ricardo ricardo.rss.dev@gmail.com'])
        }

        dockerBuildImage {
            if (project.version.toString().contains('SNAPSHOT')) {
                images = ['oac-server:latest']
            } else {
                images = ['oac-server:latest', 'oac-server:' + project.version]
            }
        }
    }
    dependsOn('clean')
    finalizedBy('build', 'dockerCreateDockerfile', 'dockerBuildImage')
}

// ------------------------------------ Local run tasks ----------------------------------

task bootRunH2(type: BootRun) {
    group = 'Application'
    dependsOn('build')

    sourceResources sourceSets.test
    mainClass = bootJar.mainClass

    doFirst() {
        systemProperty 'spring.profiles.active', 'dev, preLoad'

        h2DB.each {e ->
            systemProperty e.key, e.value
        }

        sourceSets.main.runtimeClasspath += configurations.devClasspath
        classpath = sourceSets.main.runtimeClasspath
    }
}

task bootRunMySQL(type: BootRun, dependsOn: 'build') {
    group = 'Application'
    mainClass = bootJar.mainClass

    doFirst() {
        systemProperty 'spring.profiles.active', 'dev, preLoad'

        mySqlDB.each {e ->
            logger.printf("Adding systemProp: %s = %s\n", e.key, e.value)
            systemProperty e.key, e.value
        }

        sourceSets.main.runtimeClasspath += configurations.prodClasspath
        classpath = sourceSets.main.runtimeClasspath
    }
}

